recept blaadje {
    vooruit 6
    vul kleur meng 5 delen groen met 5 delen rood met random 5 delen transparant
    cirkel 3
}

recept tak met hoogte, lengte, dikte {
    pen kleur meng 
        15 delen zwart met 
        45 delen groen met 
        3  delen rood met 
        random 5 x hoogte delen wit
        
    pen dikte dikte
    
    vooruit lengte

    // meer takken tekenen
    als hoogte > 0 {
        doe takLinks  met hoogte - 1, (lengte x 3) / 4, dikte / 2
        doe takRechts met hoogte - 1, (lengte x 3) / 4, dikte / 2
    }

    // zes blaadjes tekenen
    als hoogte <= 0 {
        herhaal 6 {
            doe blaadje
            rechts 60
        }
    }
}

recept takLinks met hoogte, lengte, dikte {
    links -60 + random 120
    doe tak met hoogte, lengte, dikte
}

recept takRechts met hoogte, lengte, dikte {
    rechts -60 + random 120
    doe tak met hoogte, lengte, dikte
}

recept boom met hoogte, lengte, dikte {
    pen kleur meng 15 delen zwart met 45 delen groen met 3  delen rood met random 50 delen wit
    pen dikte dikte
    vooruit lengte

    doe takLinks met hoogte, lengte, (dikte x 9) / 10
    doe takRechts met hoogte, lengte, (dikte x 9) / 10
}

doe boom met 7, 50, 25