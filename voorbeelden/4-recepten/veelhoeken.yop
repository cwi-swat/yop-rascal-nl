// Regelmatige veelhoeken zijn geinig. Driehoeken, vierkanten
// of zeshoeken teken je zelf makkelijk, maar voor hogere
// aantallen is een YOP recept wel zo handig. 

// Geinig: Hoe hoger het aantal hoeken, hoe meer een regelmatig veelhoek op
// een cirkel gaat lijken.
  
recept veelhoek met hoeken, stift {
    kant = 500 / hoeken       // schatting die lekker past op het scherm
    pen dikte stift
    herhaal hoeken {          // er zijn zoveel streepjes te tekenen
        vooruit kant
        links 360 / hoeken
    }
    pen dikte 2 x stift

    // aan het eind kom je precies uit waar je begonnen was. dat laten we zien met een cirkel
    cirkel 3
}

hoeken = 4

pen op
naar 0 - 200 0
pen neer
doe veelhoek met hoeken, 3

pen op
naar 0 0
pen neer
doe veelhoek met hoeken + 1, 4

pen op
naar 200 0
pen neer
doe veelhoek met hoeken + 2, 5

